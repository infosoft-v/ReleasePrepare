#Использовать json
#Использовать cmdline
// #Использовать asserts
// #Использовать strings
#Использовать 1commands
#Использовать tempfiles
#Использовать logos

Перем Лог;
Перем ПутьК1С;
Перем ПроектКлюч;
Перем ПроектФайл;
Перем ПроектИмя;
Перем КаталогБазы1С;
Перем ПользовательИмя;
Перем ПользовательПароль;
Перем КаталогЗапуска;
Перем КаталогРелизов;
Перем КаталогДампа;


// Процедура ЧтениеПараметров(МенеджерПараметров, ИмяПроекта)
//     МенеджерПараметров.УстановитьФайлПараметров("Настройки.json");
//     МенеджерПараметров.Прочитать();

//   	oscript_dir  = МенеджерПараметров.Параметр("oscript_dir");
// 	КаталогПроекта = МенеджерПараметров.Параметр(ИмяПроекта +".Каталоги.Проект");
// 	КаталогРелизов = МенеджерПараметров.Параметр(ИмяПроекта +".Каталоги.Релиз");
// 	КаталогДампа  = МенеджерПараметров.Параметр(ИмяПроекта +".Каталоги.Исходники");
// КонецПроцедуры


Функция ВзятьВКавычки(Строка)
	Возврат """" + Строка + """";
КонецФункции


Функция ЭтоСуществующийФайл(ПутьКФайлу, ПримечаниеКФайлу)
	Перем КаталогДляПоиска;

	Если СтрНайти(ПутьКфайлу, "\")=0 Тогда
		ПутьКФайлу = ОбъединитьПути(Новый Файл(ТекущийСценарий().Источник).Путь, ПутьКФайлу);
	КонецЕсли;
	Файлы = НайтиФайлы(ПутьКФайлу);
	Если Файлы.Количество()=0 Тогда
		ЛогСообщения(СтрШаблон("Отстутствует %1: %2", ПримечаниеКФайлу, ПутьКФайлу), 4, Истина);
		Возврат Ложь;
	КонецЕсли;
	Возврат Истина;
КонецФункции


// Функция СоздатьЛогВыполнения()
// 	Перем Лог;
// 	Перем ФайлЖурнала;

// 	Лог = Логирование.ПолучитьЛог("releaseprepare.app."+ПроектКлюч);
// 	ФайлЖурнала = Новый ВыводЛогаВФайл;
// 	ФайлЖурнала.ОткрытьФайл(ОбъединитьПути(КаталогЗапуска, "ReleasePrepare.log"));
// 	Лог.ДобавитьСпособВывода(ФайлЖурнала);
// 	Лог.УстановитьУровень(УровниЛога.Информация);
// 	Возврат Лог;
// КонецФункции


Функция ЛогСообщения(ТекстСообщения, УровеньСообщения, ВыводНаКонсоль)
	// Уровни логирования
	// ("Отладка"        , 0);
	// ("Информация"     , 1);
	// ("Предупреждение" , 2);
	// ("Ошибка"         , 3);
	// ("КритичнаяОшибка", 4);
	// ("Отключить"      , 5);
	Если УровеньСообщения = 0 Тогда
		Лог.Отладка(ТекстСообщения);
	ИначеЕсли УровеньСообщения = 1 Тогда
		Лог.Информация(ТекстСообщения);
	ИначеЕсли УровеньСообщения = 2 Тогда
		Лог.Предупреждение(ТекстСообщения);
	ИначеЕсли УровеньСообщения = 3 Тогда
		Лог.Ошибка(ТекстСообщения);
	ИначеЕсли УровеньСообщения = 4 Тогда
		Лог.КритичнаяОшибка(ТекстСообщения);
	Иначе
	КонецЕсли;
	Если ВыводНаКонсоль Тогда
		Сообщить(ТекстСообщения);
	КонецЕсли
КонецФункции

Функция ПолучитьТекстФайла(ФайлИмя)
	Перем оФайл, оТекст, ТекстФайла;

	ТекстФайла = "";
	оФайл = Новый Файл(ФайлИмя);
	Если оФайл.Существует() Тогда
		оТекст = Новый ЧтениеТекста(ФайлИмя, КодировкаТекста.UTF8);
		ТекстФайла = оТекст.Прочитать();
		оТекст.Закрыть();
		ОсвободитьОбъект(оТекст);
	КонецЕсли;
	Возврат ТекстФайла;
КонецФункции


Функция ТекстФайлаЗаписать(ФайлИмя, ТекстФайла)
	Перем оФайл, оТекст;
	Перем Результат;

	оФайл = Новый Файл(ФайлИмя);
	оТекст = Новый ЗаписьТекста(ФайлИмя, КодировкаТекста.UTF8);
	оТекст.Записать(ТекстФайла);
	оТекст.Закрыть();
	ОсвободитьОбъект(оТекст);
	Возврат Результат;
КонецФункции


Процедура ОбработкуРазобрать(ПутьКФайлу, КаталогДампа)
	ЛогСообщения("разбираем обработку...", 1, Истина);
	Команда = Новый Команда;
	СтрокаЗапуска = ВзятьВКавычки(ПутьК1С) + " DESIGNER"
		+ " /F" + ВзятьВКавычки(КаталогБазы1С)
		+ " /N" + ВзятьВКавычки(ПользовательИмя)
		+ " /P" + ВзятьВКавычки(ПользовательПароль)
		+ " /DumpExternalDataProcessorOrReportToFiles" + КаталогДампа
		+ " " + ПутьКФайлу;
		// + " /Out" + ОбъединитьПути(КаталогВременныхФайлов(), ПроектКлюч + "_dump.log");
	Команда.УстановитьСтрокуЗапуска(СтрокаЗапуска);
	ЛогСообщения("Запуск команды: " + СтрокаЗапуска, 1, Ложь);
	КодВозврата = Команда.Исполнить();
	ЛогСообщения("обработка разобрана; КодВозврата = "+ Строка(КодВозврата), 1, Ложь);
	// Сообщить(Команда.ПолучитьВывод());
КонецПроцедуры


Процедура ПодготовитьИсходники(Каталог)
	ЛогСообщения("Рефакторинг исходников...", 1, Истина);
	НастройкиПравил = ПолучитьНастрокиПравил();
	КоличествоПравил = НастройкиПравил.Количество();
	Если КоличествоПравил = 0 Тогда
		ЛогСообщения("Не определены правила обработки файлов.", 2, Истина);
	КонецЕсли;

	Файлы = НайтиФайлы(Каталог, "*.bsl", Истина);
	Для Каждого НайденныйФайл Из Файлы Цикл
		Если НайденныйФайл.ЭтоФайл() Тогда
			ОбработатьФайл(НайденныйФайл.ПолноеИмя, НастройкиПравил);
		КонецЕсли;
	КонецЦикла;
	ЛогСообщения("Рефакторинг завершен.", 1, Истина);
КонецПроцедуры

Функция ПолучитьИмяПроекта(КаталогДампа)
	Файлы = НайтиФайлы(КаталогДампа, "*.*", Ложь);
	Для Каждого оФайл Из Файлы Цикл
		Если оФайл.ЭтоКаталог() Тогда
			Возврат оФайл.Имя;
		КонецЕсли;
	КонецЦикла;
КонецФункции


Функция ПолучитьНастрокиПравил()
	ПутьКФайлуПравил = "RefactoringRules.json";
	ЧтениеJSON = Новый ЧтениеJSON();
	ЧтениеJSON.ОткрытьФайл(ПутьКФайлуПравил, "UTF-8");
	НастройкиПравил = ПрочитатьJSON(ЧтениеJSON, False);
	Возврат НастройкиПравил;
КонецФункции


Процедура ОбработатьФайл(ИмяФайла, НастройкиПравил)

	ЛогСообщения(" - " + СтрЗаменить(ИмяФайла, КаталогДампа, ""), 1, Истина);
	ТекстМодуля = ПолучитьТекстФайла(ИмяФайла);
	Если НастройкиПравил.Количество() > 0 Тогда
		ОтработатьПравила(ТекстМодуля, НастройкиПравил);
	КонецЕсли;

	ТекстФайлаЗаписать(ИмяФайла, ТекстМодуля);
КонецПроцедуры

Процедура ОтработатьПравила(ТекстМодуля, НастройкиПравил)
	Перем ПравилоШаблон, ПравилоЗамена, ПравилоОписание;

	Для каждого Правило из НастройкиПравил Цикл
		Если Лев(Правило.Ключ, 1) = "_" Тогда
			Продолжить;
		КонецЕсли;

		Если Правило.Значение.Свойство("Шаблон") = Неопределено Тогда
			ЛогСообщения("Не определен шаблон для правила: " + Правило.Ключ, 3, Истина);
			НастройкиПравил.Удалить(Правило.Ключ);
			Продолжить;
		КонецЕсли;
		ПравилоШаблон = Правило.Значение.Шаблон;

		Если Правило.Значение.Свойство("ЗаменитьНа") Тогда
			СтрокаЗамены = Правило.Значение.ЗаменитьНа;
		Иначе
			СтрокаЗамены = "";
		КонецЕсли;

		Если Правило.Значение.Свойство("Описание", ПравилоОписание) Тогда
			ПравилоСообщение = ПравилоОписание;
		ИначеЕсли НЕ ПустаяСтрока(СтрокаЗамены) Тогда
			ПравилоСообщение = СтрШаблон("Меняем по шаблону <%1> на <%2>", ПравилоШаблон, СтрокаЗамены);
		Иначе
			ПравилоСообщение = СтрШаблон("Удаляем по шаблону <%1>", ПравилоШаблон);
		КонецЕсли;
		ЛогСообщения(ПравилоСообщение, 1, Ложь);
		ТекстМодуля = Новый РегулярноеВыражение(ПравилоШаблон).Заменить(ТекстМодуля, СтрокаЗамены);

	КонецЦикла;

КонецПроцедуры


// Процедура УбратьКомментарии(ТекстМодуля)
// 	Перем PATTERN;
// 	PATTERN = "\s*[^:]//(?!!!).*$";

// 	ТекстМодуля = Новый РегулярноеВыражение(PATTERN).Заменить(ТекстМодуля, "");
// КонецПроцедуры


Функция ДанныеВерсии(ИмяФайла, РелизНомер)
	Перем PATTERN;
	Перем Результат;

	PATTERN = "\n\s*м_сОбработка_Версия\s*=\s*\""(\d{1,2}-\d{2}-\d{3})""\s*;";
	// PATTERN_DATE = "\n\s*м_сОбработка_Дата\s*=\s*\""(\d{2}.\d{2}.\d{4})\""\s*;";

	ТекстФайла = ПолучитьТекстФайла(ИмяФайла);

	Результат = Новый РегулярноеВыражение(PATTERN).НайтиСовпадения(ТекстФайла);
	Если Результат.Количество() = 0 Тогда
		ЛогСообщения("При поиске номера версии совпадений не найдено!", 3, Истина);
		РелизНомер = "x-xx-xxx";
	ИначеЕсли Результат.Количество() > 1 Тогда
		ЛогСообщения("При поиске номера версии найдено более одного совпадения. Берем первое.", 2, Истина);
		РелизНомер = Результат[0].Группы[1].Значение;
	Иначе
		РелизНомер = Результат[0].Группы[1].Значение;
	КонецЕсли;
	ЛогСообщения("Номер версии = " + РелизНомер, 1, Истина);
КонецФункции


Функция ПутьКФайлуМастерСборки()
	Перем ВерсияНомер;
	Перем ИмяФайлаСборки;

	ФайлДляПоиска = ОбъединитьПути(КаталогДампа, ПроектИмя, "Ext\ObjectModule.bsl");
	ДанныеВерсии(ФайлДляПоиска, ВерсияНомер);
	ИмяФайлаСборки = ОбъединитьПути(
		КаталогРелизов,
		ПроектИмя + "_" + ВерсияНомер + ".epf"
		);
	Возврат ИмяФайлаСборки;
КонецФункции


Процедура ОбработкуСобрать(ПутьКФайлуСборкиПроекта, ПутьКФайлуМастерОбработки)
	Перем ФайлЛогаКоманды;

	ФайлЛогаКоманды = ОбъединитьПути(КаталогВременныхФайлов(), ПроектИмя + "_load.log");
	Команда = Новый Команда;
	СтрокаЗапуска = ВзятьВКавычки(ПутьК1С) + " DESIGNER"
		+ " /F" + ВзятьВКавычки(КаталогБазы1С)
		+ " /N" + ВзятьВКавычки(ПользовательИмя)
		+ " /P" + ВзятьВКавычки(ПользовательПароль)
		+ " /LoadExternalDataProcessorOrReportFromFiles " + ПутьКФайлуСборкиПроекта
		+ " " + ПутьКФайлуМастерОбработки
		+ " /Out" + ФайлЛогаКоманды;

	ЛогСообщения("Формирование мастер-обработки из " + ПутьКФайлуСборкиПроекта + "...", 1, Истина);
	Команда.УстановитьСтрокуЗапуска(СтрокаЗапуска);
	КодВозврата = Команда.Исполнить();
	Если КодВозврата=0 Тогда
		ЛогСообщения("Мастер-обработка сформирована в " + ПутьКФайлуМастерОбработки, 1, Истина);
	Иначе
		ЛогСообщения("Проблемы при отработке команды сборки. Лог команды: " + ФайлЛогаКоманды, 3, Истина);
	КонецЕсли;
КонецПроцедуры


Парсер = Новый ПарсерАргументовКоманднойСтроки();
Парсер.ДобавитьИменованныйПараметр("-cfg");
Парсер.ДобавитьИменованныйПараметр("-prj");

ПараметрыЗапуска = Парсер.Разобрать(АргументыКоманднойСтроки);
ФайлНастроек = ПараметрыЗапуска["-cfg"];
ПроектКлюч = ПараметрыЗапуска["-prj"];

Лог = Логирование.ПолучитьЛог("releaseprepare.app.messages");
ФайлЖурнала = Новый ВыводЛогаВФайл;
ФайлЖурнала.ОткрытьФайл(ОбъединитьПути(КаталогЗапуска, "ReleasePrepare.log"));
Лог.ДобавитьСпособВывода(ФайлЖурнала);
Лог.УстановитьУровень(УровниЛога.Отладка);

ОшибкаЗапуска = Ложь;
Если ПроектКлюч = Неопределено ИЛИ ПустаяСтрока(ПроектКлюч) Тогда
	ЛогСообщения("Не передан ключ проекта.", 4, Истина);
	ОшибкаЗапуска = Истина;
КонецЕсли;

Если ФайлНастроек = Неопределено ИЛИ ПустаяСтрока(ФайлНастроек) Тогда
	ФайлНастроек = ОбъединитьПути(Новый Файл(ТекущийСценарий().Источник).Путь, "config.json");
	ЛогСообщения("Не передан файл настроек. Используем " + ФайлНастроек, 2, Истина);
КонецЕсли;
Если НЕ ЭтоСуществующийФайл(ФайлНастроек, "файл настроек проектов") Тогда
	ОшибкаЗапуска = Истина;
КонецЕсли;

Если ОшибкаЗапуска Тогда
	Сообщить(Символы.ПС + "Пример запуска: " + Символы.ПС);
	Сообщить("oscript ReleasePrepare.os -prj <КлючПроекта> (-cfg <ФайлНастроек>)");
	Сообщить(" <КлючПроекта>  - ключ проекта из файла настроек.");
	Сообщить("                  (Обязательный.)");
	Сообщить(" <ФайлНастроек> - имя json-файла с описанием настроек запуска для проектов.");
	Сообщить("                  (Необязательный. При отсутствии берется ""Config.json"" из каталога запуска)");
	Лог.КритичнаяОшибка("Неправильный вызов программ");
	Лог.Закрыть();
	Exit(0);
КонецЕсли;

// ПроектКлюч = "Test_Release";
КаталогЗапуска = Новый Файл(ТекущийСценарий().Источник).Путь;
ЧтениеJSON = Новый ЧтениеJSON();
ЧтениеJSON.ОткрытьФайл(ФайлНастроек, "UTF-8");
НастройкиЗапуска = ПрочитатьJSON(ЧтениеJSON, False);

Если НЕ НастройкиЗапуска.Свойство(ПроектКлюч) Тогда
	ЛогСообщения("В файле настроек отсутствует описание проекта: " + ПроектКлюч, 3, Истина);
	Лог.Закрыть();
	Exit(0);
Иначе
	НастройкиПроекта = НастройкиЗапуска[ПроектКлюч];
КонецЕсли;

ПутьК1С  = НастройкиЗапуска["Programs"]["v83"];
Если НЕ ЭтоСуществующийФайл(ПутьК1С, "файл запуска 1С") Тогда
	Лог.Закрыть();
	Exit(0)
КонецЕсли;

Если НЕ НастройкиПроекта.Свойство("ФайлОбработки") Тогда
	Сообщить("В файле настроек отсутствует описание пути к файлу обработки: " + ПроектКлюч);
	Лог.Закрыть();
	Exit(0);
КонецЕсли;
ПроектФайл = НастройкиПроекта["ФайлОбработки"];
Если НЕ ЭтоСуществующийФайл(ПроектФайл, "файл обработки") Тогда
	Лог.Закрыть();
	Exit(0)
КонецЕсли;

КаталогБазы1С = НастройкиПроекта["КаталогБазы1С"];
Если НЕ ЭтоСуществующийФайл(КаталогБазы1С, "каталог базы 1С") Тогда
	Лог.Закрыть();
	Exit(0)
КонецЕсли;
ПользовательИмя = НастройкиПроекта["Пользователь1С"]["Имя"];
ПользовательПароль = НастройкиПроекта["Пользователь1С"]["Пароль"];

КаталогДампа = ВременныеФайлы.СоздатьКаталог(ПроектКлюч);

КаталогРелизов = НастройкиЗапуска["КаталогРелизов"];
Если НЕ ЭтоСуществующийФайл(КаталогРелизов, "каталог релизов") Тогда
	Лог.Закрыть();
	Exit(0)
КонецЕсли;

Сообщить("Ключ проекта   : " + ПроектКлюч);
Сообщить("Файл проекта   : " + ПроектФайл);
// Сообщить("Путь к 1С      : " + ПутьК1С);
// Сообщить("Файл базы 1С   : " + КаталогБазы1С);
Сообщить("Имя польз.     : " + ПользовательИмя);
Сообщить("Пароль польз.  : " + ?(НЕ ПустаяСтрока(ПользовательПароль), ПользовательПароль, "без пароля")) ;
Сообщить("Каталог дампа  : " + КаталогДампа);
Сообщить("Каталог релизов: " + КаталогРелизов);

ОбработкуРазобрать(ПроектФайл, КаталогДампа);
ПроектИмя = ПолучитьИмяПроекта(КаталогДампа);

ПодготовитьИсходники(КаталогДампа);

// ПутьКФайлуСборки = ОбъединитьПути(КаталогДампа, ПроектИмя + ".xml");
// ПутьКФайлуМС = ПутьКФайлуМастерСборки();
ОбработкуСобрать(
	ОбъединитьПути(КаталогДампа, ПроектИмя + ".xml"),
	ПутьКФайлуМастерСборки()
	);

ЛогСообщения("Процесс завершен.", 1, Истина);
Лог.Закрыть();

